package EditorHelper

import NoWurst
import AbilityObjEditing
import ObjectIds
import Colors

/**
附加点: 
hand 手
foot 脚
real 后肢(四足生物)
weapon 武器 
left right
修饰手脚等附加点,添加在前面和后面都可以
head 头部
overhead 头顶
chest 胸部
origin 脚底 
turret 炮塔头部
mount 坐骑头部
eattree 吃树时树的附加点所在位置 
rally 聚集点(设置聚集点在该单位身上时的聚集点附加位置)
sprite 只有血法和建筑物有该附加点 
first second third fourth fifth sixth 修饰sprite的后缀
*/


public enum emTargetType
    None
    Unit
    Ground
    GroundorUnit

public enum emSkillKey
    Q
    W
    E
    R
    D
    F
    G

let ORDERS = ["doom", "channel", "polymorph", "farsight", "divineshield", "magicleash", "unholyfrenzy"]


class EditorHelper
    static function create_effect_faker_skill(int id, string effect_path)
        var s_editor = new AbilityDefinitionIllidanChannel(id)
        s_editor..setArtEffect(effect_path)
            ..setHotkeyNormal("")
            ..setArtCaster("")
            ..setHeroAbility(false)
            ..setLevels(1)
            ..setCooldown(1, 0)
            ..setManaCost(1, 0)
            ..setCastRange(1, 99999)
            ..setCastingTime(1, 0)
            ..setTargetType(1, 2)
            ..setArtDuration(1, 0)
            ..setFollowThroughTime(1, 0)
            ..setOptions(1, 1)
            ..setName(id.toRawCode())
        destroy s_editor

    static function create_caster_faker_unit(int id, real size)
        var u_editor = new UnitOrBuildingOrHeroDefinition(id, 'hpea')
        u_editor..setScalingValue(size)
            ..setSpeedBase(0)
            ..setScaleProjectiles(true)
            ..setTurnRate(3)
            ..setManaMaximum(9999)
            ..setManaRegeneration(9999.)
            ..setManaInitialAmount(9999)
            ..setHitPointsMaximumBase(9999)
            ..setNormalAbilities("Aloc")
            ..setShadowImageUnit("")
            ..setShadowTextureBuilding("")
            ..setHasWaterShadow(false)
            ..setShadowImageCenterX(0.)
            ..setShadowImageCenterY(0.)
            ..setShadowImageHeight(0.)
            ..setShadowImageWidth(0.)
            ..setName(id.toRawCode())
        destroy u_editor

    static function create_effect_faker_unit(int id, real size, string effect_path, color cl)
        var u_editor = new UnitOrBuildingOrHeroDefinition(id, 'hpea')
        u_editor..setScalingValue(size)
            ..setModelFile(effect_path)
            ..setSpeedBase(0)
            ..setHitPointsMaximumBase(9999)
            ..setNormalAbilities("Aloc")
            ..setTintingColorRed(cl.red)
            ..setTintingColorBlue(cl.blue)
            ..setTintingColorGreen(cl.green)
            ..setShadowImageUnit("")
            ..setShadowTextureBuilding("")
            ..setHasWaterShadow(false)
            ..setShadowImageCenterX(0.)
            ..setShadowImageCenterY(0.)
            ..setShadowImageHeight(0.)
            ..setShadowImageWidth(0.)
            ..setName(id.toRawCode())
        destroy u_editor

    
    static function create_channel_template(int id, emSkillKey k) returns AbilityDefinitionIllidanChannel
        string base_order = ORDERS[k castTo int]
        var s_editor = new AbilityDefinitionIllidanChannel(id)
        s_editor..setOptions(1, 1)
            ..setArtTarget("")
            ..setArtEffect("")
            ..setTargetAttachmentPoint("")
            ..setArtCaster("")
            ..setCasterAttachmentPoint("")
            ..setHeroAbility(false)
            ..setLevels(1)
            ..setCastingTime(1, 0)
            ..setArtDuration(1, 0)
            ..setFollowThroughTime(1, 0)
            ..setOptions(1, 1)
            ..setBaseOrderID(1, base_order)
            ..setOrderStringUseTurnOn(base_order)
            ..setDisableOtherAbilities(1,false)

        return s_editor
