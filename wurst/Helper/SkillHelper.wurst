package SkillHelper

import Unit
import MathHelper
import TimerUtils
import Effect
import Printing
import EditorHelper
import TriggerHelper
import BossHelper

public let FAKE_CASTER = 'ctfk'

class Rush
    static let default_effect = "Objects\\Spawnmodels\\Undead\\ImpaleTargetDust\\ImpaleTargetDust.mdl"
    static let unit_id = 'rush'
    construct(unit u, angle ang, real speed, real dis, bool pause, string effect_path)
        this.u = u
        this.ang = ang
        this.speed = speed
        this.dis = dis
        this.pause = pause
        this.effect_path = effect_path
        rush()

    private function rush()
        if dis<speed or u == null
            return
        if pause
            u.pause()
        u.addAbility('Aeth')
        CreateTimer()..setData(this castTo int)
            ..startPeriodic(0.03, function Rush.rush_timer)
    
    private static function rush_timer()
        timer t = GetExpiredTimer()
        var data = t.getData() castTo Rush
        vec2 pos = data.u.getPos()
        pos = MathHelper.forward(pos, data.ang, data.speed)
        unit uExplorer = createUnit(data.u.getOwner(), Rush.unit_id, pos, data.ang)
        uExplorer.setTimedLife(1)

        if not IssueBuildOrderById(uExplorer, Rush.unit_id, pos.x, pos.y) or data.moved_dis >= data.dis
            if data.pause
                data.u.unpause()
            data.u.removeAbility('Aeth')
            destroy data
            t..pause()..destr()
            return
        data.u.setPos(pos)
        flashEffect(data.effect_path, pos)
        data.moved_dis += data.speed

    unit u
    angle ang
    real speed
    real dis
    bool pause
    real moved_dis = 0
    string effect_path = Rush.default_effect

class FanShaped
    
public class Invulnerability
    trigger tr
    real armor = 0
    unit u = null
    construct(unit u)
        print("protect")
        armor = u.getArmor()
        this.u = u
        u.setArmor(233.)
        tr = CreateTrigger()..registerUnitEvent(u, EVENT_UNIT_DAMAGED)
            ..addAction(()->BlzSetEventDamage(0))

    ondestroy
        tr..disable()..destr()
        u.setArmor(armor)


public class SkillHelper
    static function rush(unit u, angle ang, real dis, real speed, bool pauseUnit, string effect_path)
        new Rush(u, ang, speed, dis, pauseUnit, effect_path)

    static function flash()
        
    static function protect(unit u) returns Invulnerability
        return new Invulnerability(u)

    static function counter_spell(unit ucaster, unit utarget) returns bool
        if BossHelper.counter(utarget)
            real speed = 64 * 0.071 / 128
            real angle = 90
            createTTEx(ucaster.getPos3with(50) - vec3(40, 0, 0), "★反制成功★", 9, ucaster.getOwner().getColor().toColor().withAlpha(255))
                ..setPermanent(false)
                ..setFadepoint(2.)
                ..setLifespan(3.)
                ..setVelocity(speed * CosBJ(angle), speed * SinBJ(angle))
            flashEffect("Abilities\\Spells\\Human\\Defend\\DefendCaster.mdl", utarget.getPos3Real())
            return true
        return false
        
    private construct()

    
@compiletime
function create_rush_explorer()
    var editor = new UnitDefinition(Rush.unit_id, 'hpea')
    editor..setNormalAbilities("Aeth,Aloc,Ahrp")
        ..setStructuresBuilt(Rush.unit_id.toRawCode())
        ..setCollisionSize(24.)
        ..setName("RUSH")
        ..setShadowImageUnit("")
        ..setArtSpecial("")
        ..setSpeedBase(0)
        ..setAttacksEnabled(0)
        ..setHitPointsMaximumBase(9999)
        ..setFoodCost(0)
        ..setUnitClassification("")
        ..setGoldCost(0)
        ..setLumberCost(0)
        ..setSightRadiusDay(0)
        ..setSightRadiusNight(0)
        ..setModelFile(".mdl")
    destroy editor

@compiletime
function create_ice_spirit_faker_caster()
    var ed = EditorHelper.create_caster_faker_unit(FAKE_CASTER, 1)
    destroy ed