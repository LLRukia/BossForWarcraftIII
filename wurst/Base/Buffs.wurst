package Buffs

import NoWurst
import HashMap
import Unit
import Printing
import PlayerUnitHelper
import initlater PlayerUnit
import ExternalDefines


public enum BuffClearType
    volatile        //
    additions       //"D" can remove
    stable          //"D" cannot remove but "purge" can
    inherent        //permanent exist

public enum BuffType
    debuffs         //--
    buffs           //++

class Buff
    protected int m_buff_id
    protected BuffClearType m_cltp
    protected BuffType m_tp
    protected IBaseSkills m_cb
    construct(int id, BuffClearType cltp, BuffType tp, IBaseSkills cb)
        m_buff_id = id
        m_cltp = cltp
        m_tp = tp
        m_cb = cb
    ondestroy


public class Buffs
    protected var set_buff = new IterableMap<int, Buff>
    protected unit m_u
    construct(unit u)
        m_u = u


    function add_buff(int id, BuffClearType cltp, BuffType tp, IBaseSkills obj)
        var temp = new Buff(id, cltp, tp, obj)
        set_buff.put(id, temp)

    function remove_buff(BuffClearType cltp, BuffType tp)
        for id in set_buff
            var bf = set_buff.get(id)
            if bf.m_tp == tp
                if set_buff.get(id).m_cltp castTo int <= cltp castTo int
                    set_buff.remove(id)
                    m_u.removeAbility(bf.m_buff_id)
                    bf.m_cb.destroy_buff(m_u)
                    destroy bf

    function remove_buff(int id)
        if set_buff.has(id)
            var bf = set_buff.get(id)
            set_buff.remove(id)
            m_u.removeAbility(id)
            bf.m_cb.destroy_buff(m_u)
            destroy bf

public function unit.add_buff(int id, BuffClearType cltp, BuffType tp, IBaseSkills ins)
    var hdl = PlayerUnitHelper.get_instance(this)
    if hdl == -1
        return
    else
        var obj = hdl castTo PlayerUnit
        if obj == null
            if _DEBUG
                print("unit.add_buff error")
            return
        obj.add_buff(id, cltp, tp, ins)

public function unit.remove_buff(int id) returns boolean
    var hdl = PlayerUnitHelper.get_instance(this)
    if hdl == -1
        this.removeAbility(id)
        return false
    else
        var obj = hdl castTo PlayerUnit
        if obj == null
            if _DEBUG
                print("unit.remove_buff error")
            return false
        obj.remove_buff(id)
        return true

public function unit.remove_buff(BuffClearType cltp, BuffType tp)
    var hdl = PlayerUnitHelper.get_instance(this)
    if hdl == -1
        return
    else
        var obj = hdl castTo PlayerUnit
        if obj == null
            if _DEBUG
                print("unit.remove_buff(multi) error")
            return
        obj.remove_buff(cltp, tp)