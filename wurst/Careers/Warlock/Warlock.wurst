package Warlock

import public PlayerUnitHelper

import ExternalDefines
import public WarlockSkill
import EditorHelper
import PlayerUnit

public class Warlock extends PlayerUnit
	construct(player pl, unit u)
		super(pl, u)
		m_self_career = warlock
		init_self()
		listen_all()
		//u.hideAbility('wlbk', true)

	override function init_self() 
		m_ls_skill.add(new ShadowCounterSpell(m_self_unit))
		m_ls_skill.add(new UnholySprint(m_self_unit))
		m_ls_skill.add(new ShadowBolt(m_self_unit))
		m_ls_skill.add(new SiphonShadow(m_self_unit))
		m_ls_skill.add(new EternalDetermination(m_self_unit))
		m_self_unit.setAbilityLevel(SiphonShadow.self_id, 5)
		m_ls_skill.add(new SoulGem(m_self_unit))
		// m_ls_skill.add(new Revive(m_self_unit))

	override function refresh()
		super.refresh()
		m_self_unit..removeAbility(ShadowBolt.self_id)..addAbility(ShadowBolt.self_id)
			..removeAbility(SiphonShadow.self_id)..addAbility(SiphonShadow.self_id)..setAbilityLevel(SiphonShadow.self_id, 5)
			..removeAbility(ShadowCounterSpell.self_id)..addAbility(ShadowCounterSpell.self_id)
			..removeAbility(EternalDetermination.self_id)..addAbility(EternalDetermination.self_id)


@compiletime
function create_warlock()
	var editor = EditorHelper.create_template_hero(WARLOCK)
	string wl_skill_list = ShadowCounterSpell.self_id.toRawCode() + "," + 
		UnholySprint.self_id.toRawCode() + "," + ShadowBolt.self_id.toRawCode() + ","+
		SiphonShadow.self_id.toRawCode() + "," + EternalDetermination.self_id.toRawCode() +
		"," + SoulGem.self_id.toRawCode() + 
		",wlau,AInv,bAnA"

	editor..setNormalAbilities(wl_skill_list)
		..setUnitSoundSet("Necromancer")
		..setProperNames(Color_Define.COLOR_WARLOCK + "术士")
		..setName("术士")
		..setModelFile("units\\undead\\Necromancer\\Necromancer")
		..setArtSpecial("units\\undead\\Necromancer\\Necromancer")
		..setAttack1ProjectileArt("Abilities\\Weapons\\SkeletalMageMissile\\SkeletalMageMissile.mdl")
		..setAttack1AttackType(AttackType.Magic)
		..setPrimaryAttribute("INT")

@compiletime
function create_wl_au()
	var editor = EditorHelper.create_aura('wlau')
	editor..setArtTarget("")
		..setName("wl")
		..setBuffs(1, "wlid")
		..setButtonPositionNormalX(0)
		..setButtonPositionNormalY(-11)
	destroy editor

@compiletime
function create_wl_id()
	var editor = new BuffDefinition('wlid', 'Bdet')
	editor..setName(1, "Warlock")
		..setTooltipNormal(1, Color_Define.COLOR_NORMAL + "Warlock")
		..setTooltipNormalExtended(1, Color_Define.COLOR_WARLOCK + "Anti-mana.")
		..setIconNormal(1, "replaceabletextures\\commandbuttons\\wl.blp")

	