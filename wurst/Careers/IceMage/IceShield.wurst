package IceShield
import IceMage
import AbilityObjEditing
import ObjectIds
import BuffObjEditing
import TriggerHelper
import SkillHelper
import TimerUtils
import PlayerUnit

public class IceShield extends IceMageSkill
    static let self_id = 'im0d'
    static let last_time = 1.5
    static let buff_id = 'imdd'
    private Invulnerability m_buff_obj = null
    private timer m_tm
    construct(unit u)
        super(u)

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==IceShield.self_id))
            ..addAction(function IceShield.cast)
            ..setData(this castTo int)


    override function destroy_buff(unit u)
        if u != m_self_unit
            if _DEBUG
                print("D Buff Error!!")
            return

        if m_buff_obj != null
            destroy m_buff_obj
            m_buff_obj = null
            (PlayerUnitHelper.get_instance(m_self_unit) castTo PlayerUnit).set_invulnerable(false)

    static function cast()
        var obj = GetTriggeringTrigger().getData() castTo IceShield

        obj.m_buff_obj = SkillHelper.protect(obj.m_self_unit)
        obj.m_self_unit.add_buff(IceShield.buff_id, BuffClearType.stable, BuffType.buffs, obj)
        obj.m_self_unit.remove_buff(BuffClearType.additions, BuffType.buffs)
        obj.m_self_unit.remove_buff(BuffClearType.additions, BuffType.debuffs)
        (PlayerUnitHelper.get_instance(obj.m_self_unit) castTo PlayerUnit).set_invulnerable(true)
        obj.m_tm = getTimer()
        obj.m_tm..setData(obj castTo int)
            ..startPeriodic(IceShield.last_time, function tm_destroy_buff)
    
    static function tm_destroy_buff()
        timer t = GetExpiredTimer()
        var obj = t.getData() castTo IceShield
        obj.clear()

    override function clear()
        if m_tm != null
            m_tm.release()
            m_tm = null
        m_self_unit.remove_buff(IceShield.buff_id)
        

@compiletime
function create_ice_shield()
    var ed = new AbilityDefinitionBeserk(IceShield.self_id)
    ed..setDamageTakenIncrease(1, 0)
        ..setLevels(1)
        ..setMovementSpeedIncrease(1, 0)
        ..setAttackSpeedIncrease(1, 0)
        ..setHotkeyNormal("D")
        ..setDurationHero(1, IceShield.last_time)
        ..setDurationNormal(1, IceShield.last_time)
        ..setManaCost(1, 5)
        ..setCooldown(1, 12)
        ..setButtonPositionNormalX(1)
        ..setButtonPositionNormalY(1)
        ..setName("IceShield")
        ..setTooltipNormal(1,
			Color_Define.COLOR_ICEMAGE + "Ice Shield" +
			Color_Define.COLOR_HOT_KEY + "(D)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "↑冰箱无敌|n" +
            Color_Define.COLOR_LAST_TIME + "→持续1.5秒。|n" +
            Color_Define.COLOR_WARNING + "★使用此技能会消除掉大部分魔法效果。|n" +
			Color_Define.COLOR_COOL_DOWN + "→CD:12s")
        ..setIconNormal("replaceabletextures\\commandbuttons\\imd.blp")
        ..setBuffs(1, IceShield.buff_id.toRawCode())
    
@compiletime
function create_ice_shield_buff()
    var ed = new BuffDefinition(IceShield.buff_id, 'Bbsk')
    ed..setArtTarget(1, "Abilities\\Spells\\Undead\\FreezingBreath\\FreezingBreathTargetArt.mdl")
    ..setTargetAttachments(1, 1)
    ..setTargetAttachmentPoint0(1, "origin")
    ..setTooltipNormal(1, Color_Define.COLOR_ICEMAGE + "Ice Shield")
    ..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "此单位免疫伤害。")
    ..setIconNormal(1, "replaceabletextures\\commandbuttons\\imd.blp")
    