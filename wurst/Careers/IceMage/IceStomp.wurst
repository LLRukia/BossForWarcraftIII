package IceStomp

import IceMage
import ExternalDefines
import EditorHelper
import TriggerHelper
import TimerUtils
import SkillHelper
import StatisticsHelper
public class IceStomp extends IceMageSkill
    static let self_id = 'im0w'
    static let effect_id = 'im1w'
    static let help_id = 'im2w'
    static let buff_id = 'im3b'
    static let dmg = 240.
    static let radius = 350.
    static let last_time = 3.
    static let inc = 12.
    private effect m_effect = null
    private effect m_effect2 = null
    private unit m_follow = null
    private trigger m_tr_start = CreateTrigger()
    private trigger m_tr_end = CreateTrigger()
    private trigger m_tr_dmg
    private timer m_tm_effect = null
    private real m_scale_size = 0.

    construct(unit u)
        super(u)

    ondestroy
        m_tr_end..disable().destr()
        m_tr_start..disable().destr()


    override function listen()
        m_tr_start..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_CHANNEL)
            ..addCondition(Filter(()->GetSpellAbilityId()==IceStomp.self_id))
            ..addAction(function IceStomp.prepare)
            ..setData(this castTo int)
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==IceStomp.self_id))
            ..addAction(function IceStomp.cast)
            ..setData(this castTo int)
        m_tr_end..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_ENDCAST)
            ..addCondition(Filter(()->GetSpellAbilityId()==IceStomp.self_id))
            ..addAction(function IceStomp.endcast)
            ..setData(this castTo int)
        
    static function prepare()
        print("prepare")
        var obj = GetTriggeringTrigger().getData() castTo IceStomp
        vec2 pos = obj.m_self_unit.getPos()
        obj.m_effect = addEffect("frozenshell.mdx", pos)
        obj.m_scale_size = 0.4
        obj.m_effect.setScale(0.4)
        obj.m_tm_effect = CreateTimer()
        obj.m_tm_effect..setData(obj castTo int)
            .startPeriodic(0.02, function effect_change)

        var fat = PlayerUnitHelper.get_instance(obj.m_self_unit) castTo IceMage
        if fat.m_self_image != null
            vec2 pos2 = fat.m_self_image.getPos()
            obj.m_effect2 = addEffect("frozenshell.mdx", pos2)
            obj.m_effect2.setScale(0.4)

    function imagecast()
        var fat = PlayerUnitHelper.get_instance(m_self_unit) castTo IceMage
        if fat.m_self_image != null and m_effect2 != null
            m_follow = createUnit(m_self_unit.getOwner(), FAKE_CASTER, fat.m_self_image.getPos(), angle(0))
            m_follow..addAbility(IceStomp.help_id)..issueImmediateOrder("thunderclap")..setXY(fat.m_self_image.getPos())
                ..setTimedLife(0.5)
            flashEffect("imw.mdx", m_follow.getPos())
            group g = CreateGroup()
            g.enumUnitsInRange(m_follow.getPos(), IceStomp.radius+AREA_DMG_CORRECTION)
            for i in g
                make_dmg(i)
            g.destr()
            m_effect2..setPos(vec2(0,0))..destr()
            m_effect2 = null
            


    static function cast()
        var obj = GetTriggeringTrigger().getData() castTo IceStomp
        obj.m_effect..setPos(vec2(0,0))..destr()
        obj.m_effect = null
        obj.imagecast()

        group g = CreateGroup()
        g.enumUnitsInRange(obj.m_self_unit.getPos(), IceStomp.radius+AREA_DMG_CORRECTION)
        for i in g
            obj.make_dmg(i)
        g.destr()

        obj.m_tm_effect..pause()..destr()
        obj.m_scale_size = 0

    static function endcast()
        var obj = GetTriggeringTrigger().getData() castTo IceStomp
        obj.m_effect..setPos(vec2(0,0))..destr()
        obj.m_effect = null

        if obj.m_effect2 != null
            obj.m_effect2..setPos(vec2(0,0))..destr()
            obj.m_effect2 = null
        if obj.m_follow != null
            RemoveUnit(obj.m_follow)
            obj.m_follow = null
        obj.m_tm_effect..pause()..destr()
        obj.m_scale_size = 0


    static function effect_change()
        timer tm = GetExpiredTimer()
        var obj = tm.getData() castTo IceStomp
        obj.m_scale_size += 0.11
        
                
        if obj.m_effect != null
            obj.m_effect.setScale(obj.m_scale_size)
            var fat = PlayerUnitHelper.get_instance(obj.m_self_unit) castTo IceMage
            if fat.m_self_image != null and obj.m_effect2 != null
                obj.m_effect2..setScale(obj.m_scale_size)..setPos(fat.m_self_image.getPos())
            else if obj.m_effect2 != null
                obj.m_effect2..setPos(vec2(0,0))..destr()
                obj.m_effect2 = null
        else
            tm..pause()..destr()
            obj.m_tm_effect = null
        
    function make_dmg(unit u)
        if not IsUnitAlly(u, GetOwningPlayer(m_self_unit))
            and u.isAlive()
            real final_dmg = GetHeroAgi(m_self_unit, true) * IceStomp.inc + IceStomp.dmg
            m_self_unit.damageTarget(u, final_dmg, ATTACK_TYPE_MAGIC)
            StatisticsHelper.add_real(m_self_unit.getOwner(), final_dmg)

@compiletime
function create_ice_stomp()
    var ed = new AbilityDefinitionMountainKingThunderClap(IceStomp.self_id)
    ed..setAttackSpeedReduction(1, .15)
    ..setSpecificTargetDamage(1, 0.)
    ..setAOEDamage(1, 0)
    ..setMovementSpeedReduction(1, .15)
    ..setAreaofEffect(1, IceStomp.radius-AREA_DMG_CORRECTION)
    ..setDurationHero(1, IceStomp.last_time)
    ..setDurationNormal(1, IceStomp.last_time)
    ..setManaCost(1, 22)
    ..setCooldown(1, 5)
    ..setCastingTime(1, 0.8)
    ..setHeroAbility(false)
    ..setArtCaster("imw.mdx")
    ..setButtonPositionNormalX(1)
    ..setButtonPositionNormalY(2)
    ..setHotkeyNormal("W")
    ..setName("IceStomp")
    ..setLevels(1)
    ..setBuffs(1, IceStomp.buff_id.toRawCode())
    ..setTooltipNormal(1,
        Color_Define.COLOR_ICEMAGE + "Ice Stomp" +
        Color_Define.COLOR_HOT_KEY + "(W)")
    ..setTooltipNormalExtended(1,
        Color_Define.COLOR_NORMAL + "↓积蓄力量，1秒后对350范围内敌人造成240伤害，并减慢他们15%/15%的速度。|n" +
        Color_Define.COLOR_ICEMAGE + "↑ 1200%法术强度额外伤害|n" +
        Color_Define.COLOR_LAST_TIME + "→减速持续3秒。|n" +
        Color_Define.COLOR_COOL_DOWN + "→CD:5s")
    ..setIconNormal("ReplaceableTextures\\CommandButtons\\BTNFreezingBreath.blp")

@compiletime
function create_ice_stomp_image()
    var ed = new AbilityDefinitionMountainKingThunderClap(IceStomp.help_id)
    ed..setAttackSpeedReduction(1, .15)
    ..setSpecificTargetDamage(1, 0.)
    ..setAOEDamage(1, 0)
    ..setMovementSpeedReduction(1, .15)
    ..setAreaofEffect(1, IceStomp.radius-AREA_DMG_CORRECTION)
    ..setDurationHero(1, IceStomp.last_time)
    ..setDurationNormal(1, IceStomp.last_time)
    ..setManaCost(1, 0)
    ..setCooldown(1, 0)
    ..setHeroAbility(false)
    ..setName("IceStomp")
    ..setLevels(1)
    ..setBuffs(1, IceStomp.buff_id.toRawCode())

@compiletime
function create_ice_stomp_buff()
    var ed = new BuffDefinition(IceStomp.buff_id, 'BHtc')
    ed..setArtTarget(1, "Abilities\\Spells\\Undead\\FreezingBreath\\FreezingBreathMissile.mdl")
    ..setTargetAttachments(1, 1)
    ..setTargetAttachmentPoint0(1, "head")
    ..setTooltipNormal(1, Color_Define.COLOR_ICEMAGE + "IceStomp")
    ..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "该单位被IceStomp击中，减速了。")
    ..setIconNormal(1, "ReplaceableTextures\\CommandButtons\\BTNFreezingBreath.blp")