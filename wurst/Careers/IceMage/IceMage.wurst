package IceMage
import public PlayerUnitHelper
import public PlayerUnit
import ExternalDefines
import HashMap
import initlater IceBarrier
import initlater IceLance
import initlater ForstBolt
import initlater ConeofCold
import public Skills

enum emSkill
	Frost_Bolt
	Cone_of_Cold
	Ice_Lance
	Ice_Barrier
	Max

public class IceMage extends PlayerUnit
	construct(player pl, unit u)
		super(pl, u)
		m_self_career = ice_mage
		init_self()

	override function spell(int skill_id)
		m_mapSkill.get(mapSkill.get(skill_id)).cast()
	
	private var mapSkill = new HashMap<int, emSkill>
	private var m_mapSkill = new HashMap<emSkill, IceMageSkill>

	ondestroy
		print("IceMage::destroy")
		for i = 0 to (emSkill.Max castTo int) - 1
			emSkill sk = i castTo emSkill
			if m_mapSkill.has(sk)
				destroy m_mapSkill.get(sk)
		destroy m_mapSkill
		destroy mapSkill

	override private function listen_all()
		for i = 0 to (emSkill.Max castTo int) - 1
			if m_mapSkill.has(i castTo emSkill)
				m_mapSkill.get(i castTo emSkill).listen()
			else
				if _DEBUG
					print("INIT NOT FINISHED YET")

	private function init_self() 
		m_mapSkill.put(emSkill.Ice_Barrier, new IceBarrier(m_self_unit))
		m_mapSkill.put(emSkill.Ice_Lance, new IceLance(m_self_unit))
		m_mapSkill.put(emSkill.Cone_of_Cold, new ConeofCold(m_self_unit))
		m_mapSkill.put(emSkill.Frost_Bolt, new ForstBolt(m_self_unit))

		mapSkill = new HashMap<int, emSkill>
		for i = 0 to (emSkill.Max castTo int) - 1
			emSkill sk = i castTo emSkill
			if m_mapSkill.has(sk)
				mapSkill.put(m_mapSkill.get(sk).get_self_id(), sk)
		listen_all()


public class IceMageSkill extends ISkill
	construct(unit spell_unit)
		super(spell_unit)

	override function cast()
		if _DEBUG
			print("IceMageSkill::error call")
		
	override function listen()

	override function get_self_id() returns int
		return 0
