package IceSpirit

import EditorHelper
import IceMage
import ExternalDefines
import BossHelper
import TriggerHelper
import HashMap
import SkillHelper
import Buffs

public class IceSpirit extends IceMageSkill
    static let self_id = 'im0f'
    static let self_ids = ['im0f', 'im1f', 'im2f', 'im3f']
    static let buff_up_id = 'im1b'
    static let buff_down_id = 'im2b'
    static let skill_up_id = 'imf1'
    static let skill_down_id = 'imf2'
    static let recovery = vec2(200., 30.)
    static let caster_faker = 'imfu'
    static let buff_up_down = vec2(0.5, 0.24)
    static var map_buff_unit = new IterableMap<timer, unit>
    static vec2 pt_cast
    private var m_map_spirit = new IterableMap<effect, rect>
    private region m_rg_judge = CreateRegion()
    private trigger m_tr_judge = CreateTrigger()
    private trigger m_tr_kill_recovery = CreateTrigger()
    private int m_exist_spirit_num = 0
    private int m_spirit_num = 0
    construct(unit u)
        super(u)
    
    ondestroy
        clear()
        
        m_rg_judge.destr()
        m_tr_judge..disable()..destr()
        m_tr_kill_recovery..disable()..destr()

        destroy m_map_spirit

    override function clear()
        m_self_unit.removeAbility(IceSpirit.self_ids[m_spirit_num])
        m_spirit_num = 0
        m_exist_spirit_num = 0
        m_self_unit.addAbility(IceSpirit.self_ids[m_spirit_num])
        for i in m_map_spirit
            m_rg_judge.clearRect(m_map_spirit.get(i))
            i..setPos(vec2(0,0))..destr()
        m_map_spirit.flush()




    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==IceSpirit.self_ids[0] or GetSpellAbilityId()==IceSpirit.self_ids[1]
            or GetSpellAbilityId()==IceSpirit.self_ids[2] or GetSpellAbilityId()==IceSpirit.self_ids[3]))
            ..addAction(function IceSpirit.cast)
            ..setData(this castTo int)
        m_tr_kill_recovery..registerAnyUnitEvent(EVENT_PLAYER_UNIT_DEATH)
            ..addCondition(Filter(function recovery_judge))
            ..addAction(function recovery)
            ..setData(this castTo int)
        m_tr_judge..registerEnterRegion(m_rg_judge, null)
            ..addAction(function IceSpirit.enter)
            ..setData(this castTo int)

    static function cast()
        location lc = GetSpellTargetLoc()
        IceSpirit.pt_cast = vec2(GetLocationX(lc), GetLocationY(lc))
        RemoveLocation(lc)
        (GetTriggeringTrigger().getData() castTo IceSpirit).cast_()

    function cast_()
        if m_spirit_num == 0
            printTimedToPlayer(Color_Define.COLOR_ICEMAGE + "Ice Spirit数量不足。", 2., m_self_unit.getOwner())
            return
        if m_exist_spirit_num == 3
            printTimedToPlayer(Color_Define.COLOR_ICEMAGE + "场上的Ice Spirit数量已经达到最大值。", 2., m_self_unit.getOwner())
            for i in m_map_spirit
                rect _rc = m_map_spirit.get(i)
                flashEffect("Abilities\\Spells\\Items\\AIam\\AIamTarget.mdl", i.getPos())
                m_map_spirit.remove(i)
                m_rg_judge.clearRect(_rc)
                RemoveRect(_rc)
                i..setPos(vec2(0,0))..destr()
                m_exist_spirit_num--
                break

        var pos = IceSpirit.pt_cast
        m_self_unit.removeAbility(IceSpirit.self_ids[m_spirit_num])
        m_spirit_num--
        m_self_unit.addAbility(IceSpirit.self_ids[m_spirit_num])
        m_exist_spirit_num++

        rect rc = Rect(pos.x - 40, pos.y - 40, pos.x + 40, pos.y + 40)
        effect e = addEffect("frostaura.mdx", pos)
        m_map_spirit.put(e, rc)
        
        m_rg_judge.addRect(rc)

    static function enter()
        var obj = GetTriggeringTrigger().getData() castTo IceSpirit
        unit u = GetEnteringUnit()
        print(u.can_add_buff())
        if (PlayerUnitHelper.get_instance(u) == -1
            and not u.isBoss()) or not u.can_add_buff()
            return
        real fmin = 10000000
        effect min_e = null
        for i in obj.m_map_spirit
            vec2 pos = u.getPos()
            real r = (i.getPos() - pos).lengthSq()
            if fmin > r
                min_e = i
                fmin = r
        
        if min_e != null
            rect rc = obj.m_map_spirit.get(min_e)
        
            obj.m_map_spirit.remove(min_e)
            obj.m_rg_judge.clearRect(rc)
            RemoveRect(rc)
            obj.m_exist_spirit_num--
            flashEffect("Abilities\\Spells\\Items\\AIam\\AIamTarget.mdl", min_e.getPos())
            min_e..setPos(vec2(0,0))..destr()
            
            unit uFaker = createUnit(obj.m_self_unit.getOwner(), FAKE_CASTER, u.getPos()+vec2(-100,0), angle(0))
            uFaker..setTimedLife(1)
                ..addAbility(IceSpirit.skill_up_id)
                ..addAbility(IceSpirit.skill_down_id)
            if u.isBoss()
                uFaker.issueTargetOrder("cripple", u)
            else
                uFaker.issueTargetOrder("bloodlust", u)
                u.add_buff(IceSpirit.buff_up_id, BuffClearType.additions, BuffType.buffs, obj)
                timer t = CreateTimer()
                map_buff_unit.put(t,u)
                t..startPeriodic(7, function IceSpirit.tmr_removebuff)

    override function destroy_buff(unit u)
        if u.isBoss()
        else
            for i in map_buff_unit
                if map_buff_unit.get(i) == u
                    i..pause()..destr()
                    map_buff_unit.remove(i)
                    break
                
        
    static function tmr_removebuff()
        var t = GetExpiredTimer()
        unit u = map_buff_unit.get(t)
        map_buff_unit.remove(t)
        u.remove_buff(IceSpirit.buff_up_id)
        t..pause()..destr()


    static function recovery_judge() returns boolean
        var obj = GetTriggeringTrigger().getData() castTo IceSpirit
        return GetKillingUnit() == obj.m_self_unit

    static function recovery()
        var obj = GetTriggeringTrigger().getData() castTo IceSpirit
        obj.m_self_unit..setHP(obj.m_self_unit.getHP() + IceSpirit.recovery.x)
                ..setMana(obj.m_self_unit.getMana() + IceSpirit.recovery.y)
        if obj.m_spirit_num < 3
            obj.m_self_unit.removeAbility(IceSpirit.self_ids[obj.m_spirit_num])
            obj.m_spirit_num++
            obj.m_self_unit.addAbility(IceSpirit.self_ids[obj.m_spirit_num])

@compiletime
function create_ice_spirit()
    let icons = ["imf0.blp","imf1.blp","imf2.blp","imf3.blp"]
    for i = 0 to 3
        var editor = EditorHelper.create_channel_template(IceSpirit.self_ids[i], emSkillKey.F)
        editor..setCastRange(1, 500)
            ..setManaCost(1, 0)
            ..setCooldown(1, 0)
            ..setButtonPositionNormalX(2)
            ..setButtonPositionNormalY(1)
            ..setHotkeyNormal("F")
            ..setTargetType(1, emTargetType.Ground castTo int)
            ..setName("IceSpirit")
            ..setTooltipNormal(1,
                Color_Define.COLOR_ICEMAGE + "Ice Spirit[Chill Version]" +
                Color_Define.COLOR_HOT_KEY + "(F)")
            ..setTooltipNormalExtended(1,
                Color_Define.COLOR_NORMAL + "被动效果：|n→当你杀死单位时，为你回复" + 
                Color_Define.COLOR_ICEMAGE + "200生命" + Color_Define.COLOR_NORMAL + "和" + 
                Color_Define.COLOR_ICEMAGE + "30魔法" + Color_Define.COLOR_NORMAL + "，并增加一点Spirit，最大3点。|n主动效果：|n→消耗一个储存的Spirit，在指定地点放置一个寒冰之灵|n" +
                Color_Define.COLOR_HOT_KEY + "↑友方玩家踩到增加50%移动速度|n" + 
                Color_Define.COLOR_WARNING  + "↓Boss踩到减少24%移动速度|n" +
                Color_Define.COLOR_LAST_TIME + "→Boss持续3秒，友方单位持续5秒。|n"+
                Color_Define. COLOR_CAST_RANGE + "→施法距离：500|n" +
                Color_Define.COLOR_COOL_DOWN + "→CD:0s")
            ..setIconNormal("replaceabletextures\\commandbuttons\\"+icons[i])

        destroy editor

@compiletime
function create_faker_skills()
    var ed1 = new AbilityDefinitionCripple(IceSpirit.skill_down_id)
    ed1..setDamageReduction(1, 0.)
        ..setAttackSpeedReduction(1, 0.)
        ..setMovementSpeedReduction(1, IceSpirit.buff_up_down.y)
        ..setDurationHero(1, 3)
        ..setDurationNormal(1, 3)
        ..setManaCost(1, 0)
        ..setCastRange(1, 99999)
        ..setCooldown(1, 0)
        ..setLevels(1)
        ..setRequirements("")
        ..setRequirementsLevels("")
        ..setTargetsAllowed(1, "enemies,ground,air,organic")
        ..setBuffs(1, IceSpirit.buff_down_id.toRawCode())
        ..setName("Spirit Boss")

    var ed2 = new AbilityDefinitionBloodlust(IceSpirit.skill_up_id)
    ed2..setScalingFactor(1, 0.)
        ..setAttackSpeedIncrease(1, 0.)
        ..setMovementSpeedIncrease(1, IceSpirit.buff_up_down.x)
        ..setDurationHero(1, 5)
        ..setDurationNormal(1, 5)
        ..setManaCost(1, 0)
        ..setCastRange(1, 99999)
        ..setCooldown(1, 0)
        ..setLevels(1)
        ..setRequirements("")
        ..setRequirementsLevels("")
        ..setBuffs(1, IceSpirit.buff_up_id.toRawCode())
        ..setName("Spirit Player")

@compiletime
function create_speed_up_buff()
    var editor = new BuffDefinition(IceSpirit.buff_up_id, 'Bdet')
    editor..setName(1, "Ice Spirit")
        ..setTooltipNormal(1, Color_Define.COLOR_ICEMAGE + "Ice Spirit")
        ..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "此单位吸收了寒冰之灵，移动速度加快了。")
        ..setArtTarget(1, "Abilities\\Spells\\Items\\AIsp\\SpeedTarget.mdl")
        ..setTargetAttachmentPoint0(1, "overhead")
        ..setIconNormal(1, "replaceabletextures\\commandbuttons\\imf0.blp")

@compiletime
function create_speed_down_buff()
    var editor = new BuffDefinition(IceSpirit.buff_down_id, 'Bdet')
    editor..setName(1, "Ice Spirit")
        ..setTooltipNormal(1, Color_Define.COLOR_ICEMAGE + "Ice Spirit")
        ..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "此单位吸收了寒冰之灵，移动速度减慢了。")
        ..setArtTarget(1, "Abilities\\Spells\\Human\\slow\\slowtarget.mdl")
        ..setIconNormal(1, "replaceabletextures\\commandbuttons\\imf0.blp")