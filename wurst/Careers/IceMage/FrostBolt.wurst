package FrostBolt
import IceMage
import EditorHelper
import ExternalDefines
import StatisticsHelper
import SkillHelper
import MathHelper

public class FrostBolt extends IceMageSkill
	static constant int self_id = 'im0q'
	static let buff_id = 'im2q'
	static let effect_id = 'im1q'
	static let fake_id = 'im3q'
	static constant real dmg = 210.
	static let inc = 8.
	
	construct(unit u)
		super(u)

	override function listen()
		m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
			..addCondition(Filter(()->GetSpellAbilityId()==FrostBolt.self_id))
			..addAction(function FrostBolt.cast)
	
	static function cast()
		unit uc = GetSpellAbilityUnit()
		unit ut = GetSpellTargetUnit()
		SkillHelper.auto_trace_missile(effect_id, ut, uc, MathHelper.get_angle(uc.getPos(), ut.getPos()),
			new FrostBoltCb(uc, ut), 0)
	
	static class FrostBoltCb implements SkillHelperCb
		unit uc
		unit ut
		construct(unit uc, unit ut)
			this.uc = uc
			this.ut = ut
		override function cb(int _obj)
			real final_dmg = GetHeroAgi(uc, true) * FrostBolt.inc + FrostBolt.dmg
			uc.damageTarget(ut, final_dmg, ATTACK_TYPE_MAGIC)
			ut.setMana(ut.getMana() - 4.)
			unit uf = createUnit(uc.getOwner(), FAKE_CASTER, uc.getPos(), angle(0))
			uf..addAbility(fake_id)..issueTargetOrder("cripple", ut)..setTimedLife(0.5)
			StatisticsHelper.add_real(uc.getOwner(), final_dmg)
			destroy this


@compiletime
function create_frost_bolt()
	var ed = EditorHelper.create_channel_template(FrostBolt.self_id, emSkillKey.Q)
	ed..setCastRange(1, 800)
		..setManaCost(1, 17)
		..setCooldown(1, 1.8)
		..setButtonPositionNormalX(0)
		..setButtonPositionNormalY(2)
		..setHotkeyNormal("Q")
		..setTargetType(1, emTargetType.Unit castTo int)
		..setTargetsAllowed(1, TYPE_ALLOWED_ENEMY)
		..setName("FrostBolt")
		..setTooltipNormal(1,
			Color_Define.COLOR_ICEMAGE + "Frost Bolt" +
			Color_Define.COLOR_HOT_KEY + "(Q)")
		..setTooltipNormalExtended(1,
			Color_Define.COLOR_NORMAL + "↓向目标射出一枚寒冰箭，造成210伤害。|n↓减少目标10%/10%的速度。|n" +
			Color_Define.COLOR_MANA_DECREASE + "↓减少目标4点能量。|n" +
			Color_Define.COLOR_ICEMAGE + "↑ 800%法术强度额外伤害|n" +
			Color_Define.COLOR_LAST_TIME + "→持续1.5秒|n" +
			Color_Define.COLOR_CAST_RANGE + "→施法距离：800|n" +
			Color_Define.COLOR_COOL_DOWN + "→CD:1.8s")
		..setIconNormal("replaceabletextures\\commandbuttons\\imq.blp")


@compiletime
function create_frost_bolt_faker()
	var editor = EditorHelper.create_cripple_template(FrostBolt.fake_id, 1.5, FrostBolt.buff_id)
	editor..setAttackSpeedReduction(1, 0.05)
		..setMovementSpeedReduction(1, 0.05)
		..setCastRange(1, 9999)
		..setManaCost(1, 0)
		..setCooldown(1, 0)
		..setDurationHero(1, 1.5)
		..setDurationNormal(1, 1.5)
		..setTargetsAllowed(1, TYPE_ALLOWED_ENEMY)
		..setName("FrostBolt")
		..setArtTarget("Abilities\\Spells\\Items\\SpellShieldAmulet\\SpellShieldCaster.mdl")
		
	destroy editor

@compiletime
function create_frost_bolt_buff()
	var editor = new BuffDefinition(FrostBolt.buff_id, 'Bdet')
	editor..setName(1, "FrostBolt")
		..setTooltipNormal(1, Color_Define.COLOR_ICEMAGE + "Frost Bolt")
		..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "此单位被寒冰箭击中，减速了。")
		..setArtTarget(1, "Abilities\\Spells\\Other\\FrostDamage\\FrostDamage.mdl")
		..setTargetAttachments(1, 0)
		..setIconNormal(1, "replaceabletextures\\commandbuttons\\imq.blp")


	destroy editor

@compiletime
function create_frost_bolt_effect()
	var ed = EditorHelper.create_effect_faker_unit(FrostBolt.effect_id, 1, "imq.mdx", color(255,255,255))
	ed.setMovementHeight(100)
	destroy ed