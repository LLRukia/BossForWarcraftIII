package IceImage

import IceMage
import EditorHelper
import GlobalDefines
import TriggerHelper
import TimerUtils
import SkillHelper
import SoundHelper

public class IceImage extends IceMageSkill
    static let self_id = 'im0g'
    static let image_skill_id = 'im1g'
    static let last_time = 10.
    private unit m_summoning = null
    private unit m_summoned = null
    private trigger m_tr_death = null
    private timer m_tm = null
    construct(unit u)
        super(u)

    ondestroy
        

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==IceImage.self_id))
            ..addAction(function IceImage.cast)
            ..setData(this castTo int)

    static function cast()
        (GetTriggeringTrigger().getData() castTo IceImage).cast_()

    function cast_()
        m_summoning = createUnit(m_self_unit.getOwner(), FAKE_CASTER, m_self_unit.getPos(), angle(0))
        m_summoning.addAbility(IceImage.image_skill_id)
        CreateTrigger()..setData(this castTo int)
            ..registerAnyUnitEvent(EVENT_PLAYER_UNIT_SUMMON)
            ..addAction(function IceImage.tr_summon)
        m_summoning.issueTargetOrderById(852274, m_self_unit)
        SoundHelper.scroll(m_self_unit.getPos3Real())
        flashEffect("scrollblue.mdx", m_self_unit.getPos())

    static function tr_summon()
        trigger tr = GetTriggeringTrigger()
        var obj = tr.getData() castTo IceImage
        if GetSummoningUnit() != obj.m_summoning
            return
        obj.m_summoned = GetSummonedUnit()
        obj.m_summoned.removeAbility('Aatk')
        
        var fat = PlayerUnitHelper.get_instance(obj.m_self_unit) castTo IceMage
        fat.m_self_image = obj.m_summoned

        obj.m_tm = getTimer()
        obj.m_tm..setData(tr.getData())
            ..startPeriodic(last_time, function tm_destroy_image)
        obj.m_tr_death = CreateTrigger()..setData(tr.getData())
            ..registerUnitEvent(obj.m_summoned, EVENT_UNIT_DEATH)
            ..addAction(function tr_destroy_image)
        RemoveUnit(obj.m_summoning)
        obj.m_summoning = null
        tr..disable()..destr()

    static function tm_destroy_image()
        var obj = GetExpiredTimer().getData() castTo IceImage
        obj.clear()
    
    static function tr_destroy_image()
        var obj = GetTriggeringTrigger().getData() castTo IceImage
        obj.clear()

    override function clear()
        if m_summoned != null
            flashEffect("Objects\\Spawnmodels\\NightElf\\NECancelDeath\\NECancelDeath.mdl", m_summoned.getPos())
            RemoveUnit(m_summoned)
            m_summoned = null
            var fat = PlayerUnitHelper.get_instance(m_self_unit) castTo IceMage
            fat.m_self_image = null
            m_tr_death..disable()..destr()
            if m_tm != null
                m_tm.release()
                m_tm = null

    


@compiletime
function create_ice_image()
    var editor = EditorHelper.create_channel_template(IceImage.self_id, emSkillKey.G)
    editor..setManaCost(1, 44)
        ..setCooldown(1, 22)
        ..setButtonPositionNormalX(3)
        ..setButtonPositionNormalY(1)
        ..setHotkeyNormal("G")
        ..setTargetType(1, emTargetType.None castTo int)
        ..setName("Image")
        ..setTooltipNormal(1,
			Color_Define.COLOR_ICEMAGE + "Image" +
			Color_Define.COLOR_HOT_KEY + "(G)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "↑产生一个自己的幻象，跟随自己一起释放W技能。幻象受到100%的伤害，造成100%的伤害。|n" +
            Color_Define.COLOR_WARNING + "★幻象造成的伤害归冰法本体所有。|n" + 
			Color_Define.COLOR_LAST_TIME + "→持续时间：10s|n" +
			Color_Define.COLOR_COOL_DOWN + "→CD:22s")
        ..setIconNormal("replaceabletextures\\commandbuttons\\img.blp")

@compiletime
function create_image_skill()
    var editor = new AbilityDefinitionItemIllusion(IceImage.image_skill_id)
    editor..setDamageReceivedMultiplier(1, 1.)
        ..setDamageDealtofnormal(1, 1.)
        ..setDurationHero(1, 100)
        ..setDurationNormal(1, 100)
        ..setCooldown(1, 0)
        ..setCastRange(1, 99999)

