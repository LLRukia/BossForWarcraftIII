package Revive

import Priest
import TriggerHelper
import EditorHelper
import ExternalDefines
import TimerUtils
import ReviveHelper

public class Revive extends PriestSkill
    static let self_id = 'pr0f'
    private trigger m_tr_target_judge = CreateTrigger()
    private trigger m_tr_end = CreateTrigger()
    private trigger m_tr_finish = CreateTrigger()
    private unit u_revive = null
    private timer m_tm
    private int m_time
    private vec2 m_tar
    construct(unit u)
        super(u)
    ondestroy
        m_tr_target_judge..disable().destr()
        m_tr_end..disable().destr()
        m_tr_finish..disable().destr()
        m_tm..pause().destr()

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==Revive.self_id))
            ..addAction(function Revive.cast)
            ..setData(this castTo int)
        m_tr_target_judge..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_CHANNEL)
            ..addCondition(Filter(()->GetSpellAbilityId()==Revive.self_id))
            ..addAction(function Revive.prepare)
            ..setData(this castTo int)
        m_tr_end..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_ENDCAST)
            ..addCondition(Filter(()->GetSpellAbilityId()==Revive.self_id))
            ..addAction(function Revive.endcast)
            ..setData(this castTo int)
        m_tr_finish..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_FINISH)
            ..addCondition(Filter(()->GetSpellAbilityId()==Revive.self_id))
            ..addAction(function Revive.finish)
            ..setData(this castTo int)

    static function finish()
        var obj = GetTriggeringTrigger().getData() castTo Revive
        if obj.m_time == 5
            if ReviveHelper.can_revive_faker(obj.u_revive)
                ReviveHelper.revive(obj.u_revive, obj.m_self_unit.getHP() / obj.m_self_unit.getMaxHP())
                flashEffect("Abilities\\Spells\\Human\\HolyBolt\\HolyBoltSpecialArt.mdl", obj.u_revive.getPos())
    
    static function cast()
        var obj = GetTriggeringTrigger().getData() castTo Revive
        obj.m_tm = CreateTimer()
        obj.m_time = 0
        flashEffect("Abilities\\Spells\\Human\\Resurrect\\ResurrectTarget.mdl", obj.u_revive.getPos())
        obj.m_tm..setData(obj castTo int)
            ..startPeriodic(1, function displaytime)
    
    static function prepare()
        var obj = GetTriggeringTrigger().getData() castTo Revive

        location lc = GetSpellTargetLoc()
        vec2 target = vec2(GetLocationX(lc), GetLocationY(lc))
        RemoveLocation(lc)

        obj.u_revive = null
        
        obj.u_revive = ReviveHelper.can_revive(target)

        if obj.u_revive == null
            printTimedToPlayer(Color_Define.COLOR_PRIEST + "请对准尸体释放。", 2, obj.m_self_unit.getOwner())
            obj.m_self_unit.issueImmediateOrder("stop")
            return

    static function displaytime()
        var obj = GetExpiredTimer().getData() castTo Revive
        if not ReviveHelper.can_revive_faker(obj.u_revive)
            obj.m_self_unit.issueImmediateOrder("stop")
            obj.m_tm..pause()..destr()
            return
        
        obj.m_time++
        real speed = 64 * 0.071 / 128
        real angle = 90
        createTTEx(obj.m_self_unit.getPos3with(120) - vec3(145, 0, 0), obj.m_time.toString(), 7, color(255,204,0).withAlpha(255))
            ..setPermanent(false)
            ..setFadepoint(2.)
            ..setLifespan(3.)
            ..setVelocity(speed * CosBJ(angle), speed * SinBJ(angle))
        flashEffect("Abilities\\Spells\\Human\\Resurrect\\ResurrectTarget.mdl", obj.u_revive.getPos())
        
        if obj.m_time == 5
            obj.m_tm..pause()..destr()
    
    static function endcast()
        var obj = GetTriggeringTrigger().getData() castTo Revive
        obj.m_tm..pause()..destr()

@compiletime
function create_revive()
    var ed = new AbilityDefinitionSeaWitchTornado(Revive.self_id)
    ed..setSummonedUnitType(1, "")
        ..setCastRange(1, 550)
        ..setManaCost(1, 55)
        ..setCooldown(1, 20)
        ..setButtonPositionNormalX(2)
        ..setButtonPositionNormalY(1)
        ..setHotkeyNormal("F")
        ..setName("Revive")
        ..setHeroAbility(false)
        ..setRequiredLevel(1)
        ..setBuffs(1, "")
        ..setDurationHero(1, 5)
        ..setDurationNormal(1, 5)
        ..setTooltipNormal(1,
        Color_Define.COLOR_PRIEST + "Revive" +
        Color_Define.COLOR_HOT_KEY + "(F)")
        ..setTooltipNormalExtended(1,
        Color_Define.COLOR_NORMAL + "→吟唱5秒后，复活目标。|n" +
        Color_Define.COLOR_CAST_RANGE + "→施法距离：550|n" +
        Color_Define.COLOR_WARNING + "▼技能被打断也会进入CD|n▲复活后目标的生命值与你的当前生命值百分比一致，且不超过50%|n" +
        Color_Define.COLOR_COOL_DOWN + "→CD:20s")
        ..setIconNormal("ReplaceableTextures\\CommandButtons\\BTNResurrection.blp")
        ..setArtTarget("Abilities\\Spells\\Human\\Heal\\HealTarget.mdl")