package HolyNova

import Priest
import TriggerHelper
import EditorHelper
import ExternalDefines

public class HolyNova extends PriestSkill
    static let self_id = 'pr0w'
    static let help_id = 'pr1w'
    static let radius = 550.
    static let pro_radius = 300.
    static let dmg = 600.
    static let inc = 10.
    private trigger m_tr_help = CreateTrigger()
    construct(unit u)
        super(u)
    ondestroy
        m_tr_help..disable().destr()

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==HolyNova.self_id))
            ..addAction(function HolyNova.cast)
            ..setData(this castTo int)
        m_tr_help..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==HolyNova.help_id))
            ..addAction(function HolyNova.cast_pro)
            ..setData(this castTo int)
    static function cast()
        var obj = GetTriggeringTrigger().getData() castTo HolyNova
        vec2 pos = obj.m_self_unit.getPos()+ vec2(16,16)
        flashEffect("prw.mdx", pos)
        group g = CreateGroup()
        g.enumUnitsInRange(pos, radius+AREA_DMG_CORRECTION)
        for i in g
            if IsUnitAlly(i, GetOwningPlayer(obj.m_self_unit)) and i.isAlive()
                real final_dmg = dmg + inc*GetHeroAgi(obj.m_self_unit, true)
                real prev = i.getHP()
                i.setHP(prev + final_dmg)
                StatisticsHelper.add_real(obj.m_self_unit.getOwner(), prev - i.getHP())

        g.destr()

    static function cast_pro()
        var obj = GetTriggeringTrigger().getData() castTo HolyNova
        vec2 pos = obj.m_self_unit.getPos() + vec2(16,16)
        addEffect("Abilities\\Spells\\Undead\\ReplenishHealth\\ReplenishHealthCasterOverhead.mdl", pos.withZ(300.))
            ..setScale(3.5)..destr()
        group g = CreateGroup()
        g.enumUnitsInRange(pos, pro_radius+AREA_DMG_CORRECTION)
        for i in g
            if IsUnitAlly(i, GetOwningPlayer(obj.m_self_unit)) and i.isAlive()
                real final_dmg = dmg + inc*GetHeroAgi(obj.m_self_unit, true)
                real prev = i.getHP()
                i.setHP(prev + final_dmg)
                StatisticsHelper.add_real(obj.m_self_unit.getOwner(), prev - i.getHP())

        g.destr()


@compiletime
function create_holy_nova()
    var editor = EditorHelper.create_channel_template(HolyNova.self_id, emSkillKey.W)
    editor..setManaCost(1, 55)
        ..setCooldown(1, 12)
        ..setButtonPositionNormalX(1)
        ..setButtonPositionNormalY(2)
        ..setHotkeyNormal("W")
        ..setTargetType(1, emTargetType.None castTo int)
        ..setName("HolyNova")
        ..setTooltipNormal(1,
            Color_Define.COLOR_PRIEST + "Holy Nova" +
            Color_Define.COLOR_HOT_KEY + "(W)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "爆发神圣能量，造成自身为圆心，半径为550的范围600点治疗。|n" + 
            Color_Define.COLOR_PRIEST + "↑ 1000%法术强度额外治疗|n" +
            Color_Define. COLOR_CAST_RANGE + "→施法距离：550|n" +
            Color_Define.COLOR_COOL_DOWN + "→CD:12s")
        ..setIconNormal("replaceabletextures\\commandbuttons\\prw.blp")

    destroy editor

@compiletime
function create_holy_nova_pro()
    var editor = EditorHelper.create_channel_template(HolyNova.help_id, emSkillKey.W)
    editor..setManaCost(1, 55)
        ..setCooldown(1, 3)
        ..setButtonPositionNormalX(1)
        ..setButtonPositionNormalY(2)
        ..setHotkeyNormal("W")
        ..setTargetType(1, emTargetType.None castTo int)
        ..setName("HolyNova")
        ..setTooltipNormal(1,
            Color_Define.COLOR_PRIEST + "Holy Nova" +
            Color_Define.COLOR_HOT_KEY + "(W)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "爆发神圣能量，造成自身为圆心，半径为300的范围600点治疗。|n" + 
            Color_Define.COLOR_PRIEST + "↑ 1000%法术强度额外治疗|n" +
            Color_Define. COLOR_CAST_RANGE + "→施法距离：300|n" +
            Color_Define.COLOR_COOL_DOWN + "→CD:3s")
        ..setIconNormal("replaceabletextures\\commandbuttons\\prw2.blp")

    destroy editor