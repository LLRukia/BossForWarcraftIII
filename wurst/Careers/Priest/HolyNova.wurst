package HolyNova

import Priest
import TriggerHelper
import MathHelper
import EditorHelper
import ExternalDefines
import TimerUtils
import SkillHelper

public class HolyNova extends PriestSkill
    static let self_id = 'pr0w'
    static let help_id = 'pr1w'
    static let radius = 700.
    static let rush_radius = 500.
    static let dmg = 600.
    static let inc = 10.
    private trigger m_tr_help = CreateTrigger()
    construct(unit u)
        super(u)

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==HolyNova.self_id))
            ..addAction(function HolyNova.cast)
            ..setData(this castTo int)
        m_tr_help..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==HolyNova.help_id))
            ..addAction(function HolyNova.cast_shock)
            ..setData(this castTo int)

    static function cast()
        var obj = GetTriggeringTrigger().getData() castTo HolyNova
        vec2 pos = obj.m_self_unit.getPos()
        flashEffect("judgement.mdx", pos)
        group g = CreateGroup()
        g.enumUnitsInRange(pos, radius)
        for i in g
            if IsUnitAlly(i, GetOwningPlayer(obj.m_self_unit)) and i.isAlive()
                real final_dmg = dmg + inc*GetHeroAgi(obj.m_self_unit, true)
                real prev = i.getHP()
                i.setHP(prev + final_dmg)
                StatisticsHelper.add_real(obj.m_self_unit.getOwner(), prev - i.getHP())

        g.destr()
        obj.m_self_unit.removeAbility(self_id)
        obj.m_self_unit.addAbility(help_id)
        CreateTimer()..setData(obj castTo int)
                ..startPeriodic(9., function reset)

    static function reset()
        var obj = GetExpiredTimer().getData() castTo HolyNova
        obj.m_self_unit.removeAbility(help_id)
        obj.m_self_unit.addAbility(self_id)

    static function cast_shock()
        var obj = GetTriggeringTrigger().getData() castTo HolyNova
        vec2 pos = obj.m_self_unit.getPos()
        flashEffect("orjnewdirtexnofire.mdx", pos)
        group g = CreateGroup()
        g.enumUnitsInRange(pos, rush_radius)
        for i in g
            if not IsUnitAlly(i, GetOwningPlayer(obj.m_self_unit)) and i.isAlive()
                angle ang = MathHelper.get_angle(pos, i.getPos())
                SkillHelper.rush(i, ang, 300, 25, false, "Objects\\Spawnmodels\\Undead\\ImpaleTargetDust\\ImpaleTargetDust.mdl")

        g.destr()

@compiletime
function create_holy_nova()
    var editor = EditorHelper.create_channel_template(HolyNova.self_id, emSkillKey.W)
    editor..setManaCost(1, 0)
        ..setCooldown(1, 9)
        ..setButtonPositionNormalX(1)
        ..setButtonPositionNormalY(2)
        ..setHotkeyNormal("W")
        ..setTargetType(1, emTargetType.None castTo int)
        ..setName("HolyNova")
        ..setTooltipNormal(1,
            Color_Define.COLOR_PRIEST + "Holy Nova" +
            Color_Define.COLOR_HOT_KEY + "(W)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "爆发神圣能量，造成自身为圆心，半径为700的范围600点治疗。当此技能进入CD时，W技能会被替换为HolyShock。|n" + 
            Color_Define.COLOR_PRIEST + "↑ 1000%法术强度额外治疗|n" +
            Color_Define. COLOR_CAST_RANGE + "→施法距离：700|n" +
            Color_Define.COLOR_COOL_DOWN + "→CD:9s")
        ..setIconNormal("HolyNova.blp")

    destroy editor

@compiletime
function create_holy_shock()
    var editor = EditorHelper.create_channel_template(HolyNova.help_id, emSkillKey.W)
    editor..setManaCost(1, 0)
        ..setCooldown(1, 30)
        ..setButtonPositionNormalX(1)
        ..setButtonPositionNormalY(2)
        ..setHotkeyNormal("W")
        ..setTargetType(1, emTargetType.None castTo int)
        ..setName("HolyShock")
        ..setTooltipNormal(1,
            Color_Define.COLOR_PRIEST + "Holy Shock" +
            Color_Define.COLOR_HOT_KEY + "(W)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "再次爆发神圣能量，造成自身为圆心，半径为500的范围击退，击退200距离|n" + 
            Color_Define. COLOR_CAST_RANGE + "→施法距离：500|n")
        ..setIconNormal("HolyShock.blp")

    destroy editor