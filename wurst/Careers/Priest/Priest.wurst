package Priest

import public PlayerUnitHelper

import ExternalDefines
import public PriestSkill
import EditorHelper
import PlayerUnit

public class Priest extends PlayerUnit
	unit m_self_image = null
	construct(player pl, unit u)
		super(pl, u)
		m_self_career = priest
		init_self()
		listen_all()
		//u.hideAbility('prbk', true)

	override function init_self() 
		m_ls_skill.add(new Heal(m_self_unit))
		m_ls_skill.add(new PowerInfusion(m_self_unit))
		m_ls_skill.add(new HolyWordShield(m_self_unit))
		m_ls_skill.add(new HolyNova(m_self_unit))
		m_ls_skill.add(new HolyMissile(m_self_unit))
		m_ls_skill.add(new DivineAegis(m_self_unit))
		m_ls_skill.add(new Revive(m_self_unit))
	override function refresh()
		super.refresh()
		m_self_unit..removeAbility(PowerInfusion.self_id)..addAbility(PowerInfusion.self_id)
			..removeAbility(HolyNova.self_id)..removeAbility(HolyNova.help_id)..addAbility(HolyNova.self_id)
			..removeAbility(DivineAegis.self_id)..addAbility(DivineAegis.self_id)
			..removeAbility(HolyMissile.self_id)..addAbility(HolyMissile.self_id)
			..removeAbility(Revive.self_id)..addAbility(Revive.self_id)

		
@compiletime
function create_priest()
	var editor = EditorHelper.create_template_hero(PRIEST)
	string pr_skill_list = Heal.self_id.toRawCode() + "," + PowerInfusion.self_id.toRawCode()
		+ "," + HolyWordShield.self_id.toRawCode() +"," + HolyNova.self_id.toRawCode()
		+"," + HolyMissile.self_id.toRawCode()+"," + DivineAegis.self_id.toRawCode()
		+"," + Revive.self_id.toRawCode() +
		",prau,AInv,bAnA"

	editor..setNormalAbilities(pr_skill_list)
		..setUnitSoundSet("Priest")
		..setProperNames(Color_Define.COLOR_PRIEST + "牧师")
		..setName("牧师")
		..setModelFile("units\\human\\Priest\\Priest")
		..setArtSpecial("units\\human\\Priest\\Priest")
		..setAttack1ProjectileArt("Abilities\\Weapons\\DruidoftheTalonMissile\\DruidoftheTalonMissile.mdl")
		..setAttack1AttackType(AttackType.Magic)
		..setPrimaryAttribute("INT")
		..setManaMaximum(300)
		..setManaInitialAmount(300)

@compiletime
function create_pr_au()
	var editor = EditorHelper.create_aura('prau')
	editor..setArtTarget("xiwa.mdx")
		..setName("pr")
		..setBuffs(1, "prid")
		..setButtonPositionNormalX(0)
		..setButtonPositionNormalY(-11)
	destroy editor

@compiletime
function create_pr_id()
	var editor = new BuffDefinition('prid', 'Bdet')
	editor..setName(1, "Priest")
		..setTooltipNormal(1, Color_Define.COLOR_NORMAL + "Priest")
		..setTooltipNormalExtended(1, Color_Define.COLOR_PRIEST + "You are an angel!")
		..setIconNormal(1, "replaceabletextures\\commandbuttons\\PR.blp")

