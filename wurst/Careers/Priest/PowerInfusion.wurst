package PowerInfusion

import Priest
import TriggerHelper
import EditorHelper
import SkillHelper
import ExternalDefines
import TimerUtils

public class PowerInfusion extends PriestSkill
    static let self_id = 'pr0g'
    static let last_time = 20.
    static let decrease = 0.3
    static let speed_up = 0.25
    static let buff_id = 'pr1g'
    static let help_id = 'pr2g'
    private trigger tr_mana_rcv = CreateTrigger()
    private timer m_tm
    construct(unit u)
        super(u)
    ondestroy
        tr_mana_rcv..disable().destr()

    override function clear()
        if m_tm != null
            m_tm.release()
        if m_self_unit.hasAbility(buff_id)
            m_self_unit.remove_buff(buff_id)

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==PowerInfusion.self_id))
            ..addAction(function PowerInfusion.cast)
            ..setData(this castTo int)
        tr_mana_rcv..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addAction(function PowerInfusion.mana_recovery)
            ..setData(this castTo int)
            ..disable()

    static function cast()
        var obj = GetTriggeringTrigger().getData() castTo PowerInfusion
        unit u =createUnit(obj.m_self_unit.getOwner(), FAKE_CASTER, obj.m_self_unit.getPos(), angle(0))
        u..addAbility(PowerInfusion.help_id)
        ..issueTargetOrder("bloodlust", obj.m_self_unit)
        ..setTimedLife(1.)

        flashEffect("scrollyell.mdx", obj.m_self_unit.getPos())
        obj.m_self_unit..add_buff(buff_id, BuffClearType.stable, BuffType.buffs, obj)
            ..removeAbility(HolyNova.self_id)
            ..addAbility(HolyNova.help_id)
        obj.tr_mana_rcv.enable()
        obj.m_tm = getTimer()
        obj.m_tm..setData(obj castTo int)
            ..startPeriodic(last_time, function tm_cb)

    override function destroy_buff(unit u)
        if u != m_self_unit
            return
        print("PowerInfusion:destroy buff")
        tr_mana_rcv.disable()
        m_self_unit..removeAbility(HolyNova.help_id)
            ..addAbility(HolyNova.self_id)
        if m_tm != null
            m_tm.release()
            m_tm = null
        
    static function tm_cb()
        var obj = GetExpiredTimer().getData() castTo PowerInfusion
        if obj.m_self_unit.hasAbility(buff_id)
            obj.m_self_unit.remove_buff(buff_id)
        obj.m_tm.release()
        obj.m_tm = null

    static function mana_recovery()
        int mana_cost = BlzGetAbilityManaCost(GetSpellAbilityId(), 1)
        var obj = GetTriggeringTrigger().getData() castTo PowerInfusion
        obj.m_self_unit.setMana(obj.m_self_unit.getMana() + mana_cost*decrease)
        if mana_cost*decrease > 1
            flashEffect("Abilities\\Spells\\Items\\AIma\\AImaTarget.mdl", obj.m_self_unit.getPos3Real())
            

    
@compiletime
function create_power_infusion()
    var ed = EditorHelper.create_channel_template(PowerInfusion.self_id, emSkillKey.G)
    ed..setHotkeyNormal("G")
        ..setManaCost(1, 5)
        ..setCooldown(1, 70)
        ..setButtonPositionNormalX(3)
        ..setButtonPositionNormalY(1)
        ..setTargetType(1, emTargetType.None castTo int)
        ..setName("PowerInfusion")
        ..setTooltipNormal(1,
			Color_Define.COLOR_PRIEST + "Power Infusion" +
			Color_Define.COLOR_HOT_KEY + "(G)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "→能量灌注->|n↑使得你速度增加25%|n↑使得你施法后返还30%魔法值|n↑使得你的神圣新星技能冷却时间减少到3秒|n↓但神圣新星技能有效范围降低为300|n↑立即刷新神圣新星的技能CD|n" +
            Color_Define.COLOR_LAST_TIME + "→持续20秒。|n" +
            Color_Define.COLOR_WARNING + "★D不会驱散此技能|n" +
			Color_Define.COLOR_COOL_DOWN + "→CD:70s")
        ..setIconNormal("replaceabletextures\\commandbuttons\\prg.blp")

@compiletime
function create_power_infusion_buff()
    var ed = new BuffDefinition(PowerInfusion.buff_id, 'Bhea')
    ed..setArtTarget(1, "prg.mdx")
    ..setTargetAttachments(1, 1)
    ..setTargetAttachmentPoint0(1, "origin")
    ..setTooltipNormal(1, Color_Define.COLOR_PRIEST + "Power Infusion")
    ..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "此单位充满了能量。")
    ..setIconNormal(1, "replaceabletextures\\commandbuttons\\prg.blp")

@compiletime
function create_power_infusion_buff_adder()
    var ed1 = new AbilityDefinitionBloodlust(PowerInfusion.help_id)
    ed1..setScalingFactor(1, 0.)
        ..setAttackSpeedIncrease(1, PowerInfusion.speed_up)
        ..setMovementSpeedIncrease(1, PowerInfusion.speed_up)
        ..setDurationHero(1, PowerInfusion.last_time)
        ..setDurationNormal(1, PowerInfusion.last_time)
        ..setManaCost(1, 0)
        ..setCastRange(1, 99999)
        ..setCooldown(1, 0)
        ..setLevels(1)
        ..setRequirements("")
        ..setRequirementsLevels("")
        ..setBuffs(1, PowerInfusion.buff_id.toRawCode())
        ..setName("PowerInfusionFaker")