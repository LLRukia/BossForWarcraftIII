package DivineAegis

import Priest
import ExternalDefines
import AbilityObjEditing
import ObjectIds
import BuffObjEditing
import TriggerHelper
import SkillHelper
import TimerUtils

public class DivineAegis extends PriestSkill
    static let self_id = 'pr0d'
    static let last_time = 1.5
    static let buff_id = 'prdd'
    private Invulnerability m_buff_obj = null
    construct(unit u)
        super(u)

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==DivineAegis.self_id))
            ..addAction(function DivineAegis.cast)
            ..setData(this castTo int)


    override function destroy_buff(unit u)
        if u != m_self_unit
            if _DEBUG
                print("D Buff Error!!")
            return

        if m_buff_obj != null
            destroy m_buff_obj
            m_buff_obj = null
            (PlayerUnitHelper.get_instance(m_self_unit) castTo PlayerUnit).set_invulnerable(false)

    static function cast()
        var obj = GetTriggeringTrigger().getData() castTo DivineAegis
        obj.m_buff_obj = SkillHelper.protect(obj.m_self_unit)
        obj.m_self_unit.add_buff(DivineAegis.buff_id, BuffClearType.stable, BuffType.buffs, obj)
        (PlayerUnitHelper.get_instance(obj.m_self_unit) castTo PlayerUnit).set_invulnerable(true)
        CreateTimer()..setData(obj castTo int)
            ..startPeriodic(DivineAegis.last_time, function tm_destroy_buff)
    
    static function tm_destroy_buff()
        timer t = GetExpiredTimer()
        var obj = t.getData() castTo DivineAegis
        obj.m_self_unit.remove_buff(DivineAegis.buff_id)
        t..pause()..destr()

@compiletime
function create_divine_aegis()
    var ed = new AbilityDefinitionBeserk(DivineAegis.self_id)
    ed..setDamageTakenIncrease(1, 0)
        ..setLevels(1)
        ..setMovementSpeedIncrease(1, 0)
        ..setAttackSpeedIncrease(1, 0)
        ..setHotkeyNormal("D")
        ..setDurationHero(1, DivineAegis.last_time)
        ..setDurationNormal(1, DivineAegis.last_time)
        ..setManaCost(1, 5)
        ..setCooldown(1, 12)
        ..setButtonPositionNormalX(1)
        ..setButtonPositionNormalY(1)
        ..setName("DivineAegis")
        ..setTooltipNormal(1,
			Color_Define.COLOR_PRIEST + "Divine Aegis" +
			Color_Define.COLOR_HOT_KEY + "(D)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "↑神圣庇护|n" +
            Color_Define.COLOR_LAST_TIME + "→持续1.5秒。|n" +
            Color_Define.COLOR_WARNING + "★使用此技能会消除掉大部分魔法效果。|n" +
			Color_Define.COLOR_COOL_DOWN + "→CD:12s")
        ..setIconNormal("DivineAegis.blp")
        ..setBuffs(1, DivineAegis.buff_id.toRawCode())
    
@compiletime
function create_ice_shield_buff()
    var ed = new BuffDefinition(DivineAegis.buff_id, 'Bbsk')
    ed..setArtTarget(1, "Abilities\\Spells\\Items\\StaffOfSanctuary\\Staff_Sanctuary_Target.mdl")
    ..setTargetAttachments(1, 1)
    ..setTargetAttachmentPoint0(1, "chest")
    ..setTooltipNormal(1, Color_Define.COLOR_PRIEST + "Divine Aegis")
    ..setTooltipNormalExtended(1, Color_Define.COLOR_NORMAL + "此单位免疫伤害。")
    ..setIconNormal(1, "DivineAegis.blp")
    