package ArcaneMissile

import ArcaneMage
import EditorHelper
import SoundUtils
import GlobalDefines
import SkillHelper

public class ArcaneMissile extends ArcaneMageSkill
    static let self_id = 'am0q'
    static let effect_id = 'am1q'
    static let inc = 5.
    static let dmg = 200.
    static var launch = new SoundDefinition(Sounds.faerieDragonAttack, false, true)
    construct(unit u)
        super(u)

    override function listen()
        m_tr_self..registerUnitEvent(m_self_unit, EVENT_UNIT_SPELL_EFFECT)
            ..addCondition(Filter(()->GetSpellAbilityId()==ArcaneMissile.self_id))
            ..addAction(function ArcaneMissile.cast)

    static function cast()
        unit uc = GetSpellAbilityUnit()
        unit ut = GetSpellTargetUnit()
        angle ang
        if GetRandomInt(1, 2) == 1
            ang = GetRandomReal(-60., -30.).asAngleDegrees()
        else
            ang = GetRandomReal(30., 60.).asAngleDegrees()

        launch.playOnPoint(uc.getPos3Real())
        SkillHelper.auto_trace_missile_modify_speed(effect_id, ut, uc, ang + uc.getFacingAngle(),
            new ArcaneMissileCb(uc, ut), 0, 55., 11., 55., 0)
    
public class ArcaneMissileCb implements SkillHelperCb
    unit uc
    unit ut
    construct(unit uc, unit ut)
        this.uc = uc
        this.ut = ut
    override function cb(int _obj)
        real final_dmg = GetHeroAgi(uc, true) * ArcaneMissile.inc + ArcaneMissile.dmg
        uc.damageTarget(ut, final_dmg, ATTACK_TYPE_MAGIC)
        flashEffect("amq1.mdx", ut.getPos3Real())
        flashEffect("amq2.mdx", ut.getPos3Real())
        ut.setMana(ut.getMana() - 3.)
        StatisticsHelper.add_real(uc.getOwner(), final_dmg)
        destroy this

@compiletime
function create_arcane_bolt()
    var ed = EditorHelper.create_channel_template(ArcaneMissile.self_id, emSkillKey.Q)
    ed..setCastRange(1, 800)
        ..setManaCost(1, 22)
        ..setCooldown(1, 1.8)
        ..setButtonPositionNormalX(0)
        ..setButtonPositionNormalY(2)
        ..setHotkeyNormal("Q")
        ..setTargetType(1, emTargetType.Unit castTo int)
        ..setTargetsAllowed(1, TYPE_ALLOWED_ENEMY)
        ..setName("ArcaneMissile")
        ..setTooltipNormal(1,
            Color_Define.COLOR_ICEMAGE + "Arcane Missile" +
            Color_Define.COLOR_HOT_KEY + "(Q)")
        ..setTooltipNormalExtended(1,
            Color_Define.COLOR_NORMAL + "→向目标射出一枚奥术飞弹，造成200伤害。|n" +
            Color_Define.COLOR_MANA_DECREASE + "↓减少目标3点能量。|n" +
            Color_Define.COLOR_ICEMAGE + "↑ 500%法术强度额外伤害|n" +
            Color_Define.COLOR_CAST_RANGE + "→施法距离：800|n" +
            Color_Define.COLOR_COOL_DOWN + "→CD:1.8s")
        ..setIconNormal("replaceabletextures\\commandbuttons\\amq.blp")

@compiletime
function create_arcane_bolt_effect()
    var ed = EditorHelper.create_effect_faker_unit(ArcaneMissile.effect_id, 1, "amq.mdx", color(255,255,255))
    ed.setMovementHeight(100)
    destroy ed